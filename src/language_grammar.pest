WHITESPACE = _{ " " }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
integer = @{ "-"? ~ ASCII_DIGIT+ }
decimal = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
variable = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
string = @{ "'" ~ ( "''" | (!"'" ~ ANY) )* ~ "'" }
array = { "[" ~ (types ~ ",")* ~ types? ~ "]" }
entry = { string ~ ":" ~ expression }
dictionary = { "{" ~ (entry ~ ",")* ~ entry? ~ "}" }

types = _{ decimal | integer | string | dictionary | array }

verb = { "+" | "×" | "÷" | "print" | "⍳" | "⍴" | "/" | ">" | "¨" | "." }

assignment = { variable ~ "<-" ~ expression }
expression = { assignment | operatorExpression | monadicExpression | dyadicExpression | terms | string }
operatorExpression = { verb ~ verb ~ expression | "(" ~ verb+ ~ ")" ~ verb ~ expression }
monadicExpression = { verb ~ expression }
dyadicExpression = { (monadicExpression | terms) ~ verb ~ expression }
term = _{ decimal | integer | string | variable | array | dictionary | "(" ~ expression ~ ")" }
terms = { term+ }

program = _{ SOI ~ "\n"* ~ (expression ~ "\n"+)* ~ expression? ~ EOI }